; "this file contains:"
; "   object definitions of special objects"
; "   object definitions of global  objects"
; "   object definitions of generic objects"
; "   object definitions of game    objects"

; "special objects"

<OBJECT TH-X-OBJECT>
<OBJECT TH-Y-OBJECT>

; "GLOBAL-OBJECTS"

<OBJECT PSEUDO-OBJECT
	(LOC GLOBAL-OBJECTS)	; "This fakes out some verb routines"
	(FLAGS FL-NOARTC FL-HAS-SDESC FL-HAS-DESCFCN)
	(ACTION 0)>

<OBJECT GLOBAL-OBJECTS
   (FLAGS
      FL-ALIVE
      FL-ASLEEP
      FL-BODYPART
      FL-BROKEN
      FL-BURNABLE
      FL-BUYABLE
      FL-BYHAND
      FL-CLOTHING
      FL-COLLECTIVE
      FL-CONTAINER
      FL-DOOR
      FL-FEMALE
      FL-FOOD
      FL-HOLMES-TOLD
      FL-INDOORS
      FL-KEY
      FL-KNIFE
      FL-LAMP
      FL-LIGHTED
      FL-LOCATION
      FL-LOCKED
      FL-MONEY
      FL-NOALL
      FL-NOARTC
      FL-NODESC
      FL-OPENABLE
      FL-OPENED
      FL-PERSON
      FL-PLURAL
      FL-READABLE
      FL-SEEN
      FL-SURFACE
      FL-TAKEABLE
      FL-TOOL
      FL-TOUCHED
      FL-TRANSPARENT
      FL-TRYTAKE
      FL-VEHICLE
      FL-VOWEL
      FL-WEAPON
      FL-WORN
      FL-YOUR
      FL-HAS-DESCFCN
      FL-HAS-SDESC
      FL-NO-CROWDS
      FL-NO-ETHERIUM 
      ;"Set in indoor rooms where don't allow breaking ampoule")>

; "Note that FL-NEEDS-IDENTITY and FL-LOSE-IDENTITY aren't included above..."

<OBJECT LOCAL-GLOBALS
   (LOC GLOBAL-OBJECTS)
   ;(GLOBAL GLOBAL-OBJECTS)
   ;(SYNONYM ZZZP)
   ;(ADJECTIVE XXXP)
   ;(NORTH TO ROOMS)
   ;(EAST  TO ROOMS)
   ;(SOUTH TO ROOMS)
   ;(WEST  TO ROOMS)
   ;(NE    TO ROOMS)
   ;(NW    TO ROOMS)
   ;(SE    TO ROOMS)
   ;(SW    TO ROOMS)
   ;(IN    TO ROOMS)
   ;(OUT   TO ROOMS)
   ;(UP    TO ROOMS)
   ;(DOWN  TO ROOMS)
   ;(ACTION		0)
   ;(CONTFCN		0)
   ;(GENERIC		0)
   ;(VALUE		0)
   ;(SIZE		0)
   ;(LOOKUP		0)
   ;(LOOKDN		0)
   ;(ODOR		0)
   ;(INSCRIPTION	0)
   ;(FLIPPED		0)
   ;(HEATED		0)
   ;(STRENGTH		0)
   ;(DEXTERITY		0)>

<OBJECT ROOMS
   (DESC "rooms")
   (FLAGS FL-NOARTC FL-NODESC FL-SEEN)
   (IN TO ROOMS)>

<OBJECT TH-ROOM
   (LOC GLOBAL-OBJECTS)
   (DESC "room")
   (FLAGS FL-NODESC FL-SEEN FL-HAS-DESCFCN)
   (SYNONYM ROOM)
   (ACTION RT-DF-TH-ROOM)>

<OBJECT TH-INTDIR
   (LOC GLOBAL-OBJECTS)
   (DESC "direction")
   (FLAGS FL-NODESC FL-SEEN)
   (SYNONYM DIRECTION)
   (ADJECTIVE NORTH EAST SOUTH WEST NE NW SE SW UP DOWN IN OUT)
   (ACTION RT-AC-TH-INTDIR)>

<OBJECT TH-NOT-HERE-OBJECT
   (DESC "that")
   (FLAGS FL-NOARTC FL-NODESC FL-SEEN)
   (ACTION RT-AC-TH-NOT-HERE-OBJECT)>

<OBJECT TH-TIME
   (LOC GLOBAL-OBJECTS)
   (SYNONYM MINUTE MINUTES HOUR HOURS)
   (ADJECTIVE INTNUM)
   (FLAGS FL-HAS-SDESC FL-NEEDS-IDENTITY)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (ACTION RT-AC-TH-TIME)>

<OBJECT TH-SESAME
   (LOC GLOBAL-OBJECTS)
   (DESC "sesame")
   (FLAGS FL-NODESC)
   (SYNONYM SESAME)
   (ACTION RT-AC-TH-SESAME)>

<OBJECT TH-IT
   (LOC GLOBAL-OBJECTS)
   (DESC "it")
   (FLAGS FL-NOARTC FL-NODESC FL-SEEN FL-VOWEL FL-TOUCHED)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (SYNONYM IT THAT ITSELF)>

<OBJECT TH-RIGHT
   (LOC GLOBAL-OBJECTS)
   (DESC "right")
   (FLAGS FL-NOARTC FL-NODESC FL-SEEN)
   (SYNONYM RIGHT FORWARD AHEAD CLOCKWISE)
   (ACTION RT-AC-TH-RIGHT)>

<OBJECT TH-LEFT
   (LOC GLOBAL-OBJECTS)
   (DESC "left")
   (FLAGS FL-NOARTC FL-NODESC FL-SEEN)
   (SYNONYM LEFT BACKWARD BEHIND COUNTERCL)
   (ACTION RT-AC-TH-LEFT)>

<OBJECT TH-FLOOR-ETC
   (LOC GLOBAL-OBJECTS)
   (FLAGS FL-NODESC FL-SEEN FL-SURFACE FL-NEEDS-IDENTITY
          FL-HAS-DESCFCN FL-HAS-SDESC)
   (SYNONYM FLOOR CEILING SURFACE GROUND SKY CLOUD CLOUDS FOG)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (ACTION RT-AC-TH-FLOOR-ETC)>

<ROUTINE RT-IDENTIFY-FLOOR? (WD "OPT" (OBJ <>)
			     "AUX" (NAM <GETP ,TH-FLOOR-ETC ,P?OBJ-NOUN>))
  <COND (<AND .OBJ <N==? .OBJ ,TH-FLOOR-ETC>>
	 <RFALSE>)>
  <COND (<EQUAL? .WD ,W?FLOOR>
	 <EQUAL? .NAM ,W?FLOOR>)
	(<EQUAL? .WD ,W?CEILING>
	 <EQUAL? .NAM ,W?CEILING>)
	(<EQUAL? .WD ,W?SKY>
	 <EQUAL? .NAM ,W?SKY ,W?CLOUD ,W?CLOUDS ,W?FOG>)
	(<EQUAL? .WD ,W?GROUND>
	 <EQUAL? .NAM ,W?SURFACE ,W?GROUND>)>>

<ROUTINE TELL-THE-FLOOR ()
  <TELL "the floor">>

<ROUTINE TELL-THE-GROUND ()
  <TELL "the ground">>

<ROUTINE RT-AC-TH-FLOOR-ETC ("OPT" (CONTEXT <>) "AUX" (INDOORS? <>) (UP? <>)
			      OBJ NXT NAM (THING ,TH-FLOOR-ETC) TOD)
  <COND (<==? .CONTEXT ,K-M-DESCFCN>
	 <COND (<AND <==? <GETP ,TH-FLOOR-ETC ,P?OBJ-NOUN> ,W?CEILING>
		     <EQUAL? ,GL-PLACE-CUR ,RM-HENRY-VII-CHAPEL>>
		<TELL ,K-BREATH-TAKING-MSG CR>
		<RTRUE>)
	       (T
		<RFALSE>)>)
	(<==? .CONTEXT ,K-M-SDESC>
	 <PRINTB <GETP .THING ,P?OBJ-NOUN>>
	 <RTRUE>)
	(<MC-T? .CONTEXT>
	 <RFALSE>)
	(<EQUAL? <SET NAM <GETP .THING ,P?OBJ-NOUN>> ,W?FLOOR
		 ,W?CEILING>
	 <SET INDOORS? T>)>
  <COND (<OR <RT-IDENTIFY-FLOOR? ,W?CEILING>
	     <RT-IDENTIFY-FLOOR? ,W?SKY>>
	 <SET UP? T>)>
  <COND (<EQUAL? .INDOORS? <MC-ISNOT? ,GL-PLACE-CUR ,FL-INDOORS>>
	 <RT-CANT-SEE-ANY-MSG .THING>
	 <RFATAL>)
	(<MC-THIS-PRSI?>
	 <COND (<MC-VERB? PUT-ON PUT THROW THROW-OVER EMPTY-INTO>
		<COND (<NOT .UP?>
		       <RT-PERFORM ,V?DROP ,GL-PRSO>)
		      (T
		       <TELL "Despite your best efforts, " THEO
			     " just won't stay up." CR>)>)>)
	(<AND <NOT .UP?> <MC-THIS-PRSO?>>
	 <COND (<MC-VERB? CROSS>
		<V-WALK-AROUND>)
	       (<MC-VERB? EXAMINE LOOK-ON SEARCH>
		<COND (<AND <FSET? .THING ,FL-HAS-DESCFCN>
			    <APPLY <GETP .THING ,P?ACTION> ,K-M-DESCFCN>>
		       <RTRUE>)>
		<RT-REMOVE-ALL ,TH-X-OBJECT>
		<SET OBJ <FIRST? ,GL-PLACE-CUR>>
		<REPEAT ()
		  <COND (.OBJ
			 <SET NXT <NEXT? .OBJ>>
			 <COND (<OR <MC-IS? .OBJ ,FL-NODESC>
				    <AND <MC-IS? .OBJ ,FL-ALIVE>
					 <MC-ISNOT? .OBJ ,FL-ASLEEP>>
				    <AND <MC-ISNOT? .OBJ ,FL-TAKEABLE>
					 <MC-ISNOT? .OBJ ,FL-TRYTAKE>>>
				<MOVE .OBJ ,TH-X-OBJECT>)>
			 <SET OBJ .NXT>)
			(T
			 <RETURN>)>>
		<COND (<FIRST? ,GL-PLACE-CUR>
		       <RT-CYOU-MSG "see" "sees">
		       <RT-PRINT-CONTENTS-2 ,GL-PLACE-CUR>)
		      (T
		       <TELL "There is nothing very "
			     <RT-PICK-NEXT ,GL-BORING-TXT>>)>
		<TELL " on " THE .THING "." CR>
		<RT-MOVE-ALL ,TH-X-OBJECT ,GL-PLACE-CUR>)>)
	(<MC-THIS-PRSO?>
	 <COND (<MC-VERB? EXAMINE LOOK-INSIDE SEARCH>
		<COND (<EQUAL? ,GL-PLACE-CUR ,RM-HENRY-VII-CHAPEL>
		       <TELL CTHE .THING " is breathtakingly beautiful." CR>)
		      (<MC-IS? ,GL-PLACE-CUR ,FL-INDOORS>
		       <TELL CTHE .THING " is high above you." CR>
		       <RTRUE>)
		      (T
		       <TELL CTHE .THING>
		       <SET TOD <RT-TIME-OF-DAY>>
		       <COND (<EQUAL? .TOD 0>
			      <TELL " is dark and foggy." CR>)
			     (<EQUAL? .TOD 1>
			      <TELL " is foggy, but at least there's enough light to see." CR>)
			     (<EQUAL? .TOD 2>
			      <TELL " is pretty light." CR>)
			     (<EQUAL? .TOD 3>
			      <TELL " is getting darker." CR>)>)>)
	       (<MC-VERB? SHOOT>
		<COND (<MC-ISNOT? ,GL-PLACE-CUR ,FL-INDOORS>
		       <TELL "The bullet disappears into the air." CR>)>)>)>>

<OBJECT TH-HANDS
   (LOC GLOBAL-OBJECTS)
   (DESC "hands")
   (FLAGS FL-SEEN FL-NODESC FL-TOUCHED FL-BYHAND FL-TOOL FL-PLURAL
    	  FL-BODYPART FL-YOUR FL-NOALL)
   (SYNONYM HAND HANDS FINGER FINGERS)
   (ADJECTIVE MY)
   (ACTION RT-AC-TH-HANDS)>

<OBJECT TH-WATSONS-HEART
   (LOC GLOBAL-OBJECTS)
   (DESC "heart")
   (FLAGS FL-SEEN FL-NODESC FL-BODYPART FL-YOUR FL-NOALL)
   (SYNONYM HEART HEARTBEAT)
   (ADJECTIVE MY)
   (ACTION RT-AC-TH-WATSONS-HEART)>

<OBJECT TH-FEET
   (LOC GLOBAL-OBJECTS)
   (DESC "feet")
   (FLAGS FL-SEEN FL-NODESC FL-PLURAL FL-BODYPART FL-YOUR FL-NOALL)
   (SYNONYM FEET FOOT)
   (ADJECTIVE MY)>

<OBJECT TH-EARS
   (LOC GLOBAL-OBJECTS)
   (DESC "ears")
   (FLAGS FL-SEEN FL-NODESC FL-CONTAINER FL-SURFACE FL-OPENED
	  FL-PLURAL FL-BODYPART FL-YOUR FL-NOALL)
   (SYNONYM EAR EARS)
   (ADJECTIVE MY)
   (SIZE 0 CAPACITY 1)
   (ACTION RT-AC-TH-EARS)>

<OBJECT TH-HEAD
	(LOC GLOBAL-OBJECTS)
	(DESC "head")
	(FLAGS FL-SEEN FL-NODESC FL-SURFACE FL-BODYPART FL-YOUR FL-NOALL)
	(SYNONYM HEAD)
	(ADJECTIVE MY)
	(ACTION RT-AC-TH-HEAD)>

<OBJECT TH-PLAYER-BODY
   (LOC GLOBAL-OBJECTS)
   (DESC "that part of your body")
   (SYNONYM ARM ARMS SHOULDER SHOULDERS LEG LEGS FACE MOUTH LIPS THROAT
            EYE EYES NOSE HAIR STOMACH BODY)
   (FLAGS FL-NOARTC FL-SEEN FL-NODESC FL-NOALL FL-BODYPART)
   (ADJECTIVE MY LEFT RIGHT)
   (ACTION RT-AC-TH-PLAYER-BODY)>

<OBJECT TH-BREATH
	(LOC GLOBAL-OBJECTS)
	(DESC "breath")
	(FLAGS FL-SEEN FL-NODESC FL-TRYTAKE FL-NOALL FL-YOUR FL-BODYPART)
	(SYNONYM BREATH)
	(ADJECTIVE MY DEEP)
	(ACTION RT-AC-TH-BREATH)>

<OBJECT TH-CLOTHES
	(LOC GLOBAL-OBJECTS)
	(DESC "clothes")
	(FLAGS FL-SEEN FL-NODESC FL-CLOTHING FL-WORN FL-YOUR FL-NOALL)
	(SYNONYM CLOTHES CLOTHING)
	(GENERIC RT-GN-TH-CLOTHES)
	(SIZE 100)
	(ADJECTIVE MY)
	(ACTION RT-AC-TH-CLOTHES)>

<OBJECT TH-WATERMARK
	(LOC GLOBAL-OBJECTS)
	(DESC "watermark")
	(FLAGS FL-NODESC FL-HAS-DESCFCN)
	(SYNONYM WATERMARK MARK)
	(ACTION RT-AC-TH-WATERMARK)>

<OBJECT TH-BULLET-HOLE
	(LOC GLOBAL-OBJECTS)
	(DESC "bullet hole")
	(SYNONYM HOLE HOLES)
	(ADJECTIVE BULLET)
	(ACTION RT-AC-TH-BULLET-HOLE)>

; "BODIES FOR WIGGINS, HOLMES, HUDSON, AND ELIZA.  WORKS WELL BECAUSE ALL
   BODY PARTS ALSO EXIST FOR PLAYER, WHO IS ALWAYS PREFERRED BY THE GENERIC,
   AND ELIZA'S HEART WILL BE FOUND BEFORE GLOBAL-CHECK IS CALLED (I THINK)."

<OBJECT TH-GENERIC-BODY
	(LOC GLOBAL-OBJECTS)
	(SYNONYM ARM ARMS SHOULDER SHOULDERS LEG LEGS HAND
	 HANDS FOOT FEET HEAD FACE MOUTH LIPS THROAT EYE EYES EAR EARS
	 NOSE HAIR HEART STOMACH BODY CLOTHES)
	(ADJECTIVE WIGGINS\' HOLMES\' HOLMES\'S HUDSON\'S LEFT RIGHT
		    HER ELIZA\'S GIRL\'S)
	(GENERIC RT-GN-BODY)
	(OBJ-ADJ 0)
	(OBJ-NOUN 0)
	(FLAGS FL-NOARTC FL-NODESC FL-HAS-SDESC FL-NEEDS-IDENTITY)
	(ACTION RT-AC-TH-GENERIC-BODY)>

; "UNION OF GLOBAL N,S,E,W WALLS, LOCAL GLOBAL N,S,E,W WALLS, AND
   GLOBAL WALLS"

<OBJECT TH-WALL
	(LOC GLOBAL-OBJECTS)
	(FLAGS FL-TRYTAKE FL-NOALL FL-NEEDS-IDENTITY FL-HAS-SDESC)
	(OBJ-ADJ 0)
	(OBJ-NOUN 0)
	(SYNONYM WALL WALLS)
	(ADJECTIVE NORTH SOUTH EAST WEST)
	(ACTION RT-AC-TH-WALL)>

<ROUTINE RT-AC-TH-WALL ("OPT" (CONTEXT <>) "AUX" (OBJ ,TH-WALL)
			(NAM <GETP .OBJ ,P?OBJ-NOUN>)
			(ADJ <GETP .OBJ ,P?OBJ-ADJ>))
  <COND (<==? .CONTEXT ,K-M-SDESC>
	 <COND (<GETP .OBJ ,P?OBJ-ADJ>
		<PRINTB .OBJ>
		<TELL " ">)>
	 <PRINTB <GETP .OBJ ,P?OBJ-NOUN>>
	 <RTRUE>)
	(<T? .CONTEXT> <RFALSE>)>
  <COND (<==? .NAM ,W?WALLS>
	 ; "Plural"
	 <COND (<MC-IS? ,GL-PLACE-CUR ,FL-INDOORS>
		<COND (<MC-VERB? SHOOT>
		       <PUTP .OBJ ,P?OBJ-NOUN ,W?WALL>
		       <PUTP .OBJ ,P?OBJ-ADJ ,W?SOUTH>
		       <RFALSE>)
		      (<MC-VERB? EXAMINE LOOK-ON>
		       <COND (<MC-HERE? ,RM-HOLMES-STUDY>
			      <TELL
			       "The walls are covered with pictures.">)>)>)
		(<MC-VERB? EXAMINE LOOK-ON>
		 <COND (<MC-HERE? ,RM-INSIDE-TRAITORS-GATE>
			<TELL "There are some ravens perched on the wall.">)>)
		(<EQUAL? ,GL-PLACE-CUR ,RM-TOTTENHAM-COURT-RD
			 ,RM-OXFORD-ST ,RM-VICTORIA-SQUARE
			 ,RM-VICTORIA-STREET ,RM-PARLIAMENT-SQUARE
			 ,RM-QUEENS-GARDENS ,RM-BUCKINGHAM-PALACE
			 ,RM-DRAWBRIDGE ,RM-BYWARD-TOWER ,RM-OUTER-WARD
			 ,RM-TOWER-GREEN ,RM-OUTSIDE-TRAITORS-GATE>
		 <RFALSE>)
		(T
		 <TELL ,K-NOT-SEE-HERE-MSG CR>)>)
	(<AND <EQUAL? .ADJ ,W?NORTH>
	      <EQUAL? ,GL-PLACE-CUR ,RM-MARYLEBONE-RD
		      ,RM-GREAT-RUSSELL-ST ,RM-THREADNEEDLE-ST
		      ,RM-MONUMENT ,RM-OUTSIDE-TRAITORS-GATE>>
	 ; "FORMER LG-NORTH-WALL"
	 <COND (<MC-VERB? EXAMINE LOOK-ON>
		<COND (<MC-HERE? ,RM-GREAT-RUSSELL-ST ,RM-THREADNEEDLE-ST
				 ,RM-MONUMENT>
		       <TELL ,K-SIGN-HERE-MSG>)>)>)
	(<AND <EQUAL? .ADJ ,W?EAST>
	      <EQUAL? ,GL-PLACE-CUR ,RM-BROAD-SANCTUARY
		      ,RM-WHITEHALL ;,RM-BLIND-ALLEY>>
	 ; "LG-EAST-WALL"
	 <COND (<MC-VERB? EXAMINE LOOK-ON>
		<COND (<MC-HERE? ,RM-BROAD-SANCTUARY ;,RM-BLIND-ALLEY>
		       <TELL ,K-SIGN-HERE-MSG>)>)>)
	(<AND <EQUAL? .ADJ ,W?WEST>
	      <EQUAL? ,GL-PLACE-CUR ,RM-221B-BAKER-ST
		      ,RM-TRAFALGAR-SQUARE ,RM-PINCHIN-LANE
		      ,RM-SWAN-LANE>>
	 ; "LG-WEST-WALL"
	 <COND (<MC-VERB? EXAMINE LOOK-ON>
		<COND (<MC-HERE? ,RM-SWAN-LANE>
		       <TELL ,K-SIGN-HERE-MSG>)>)>)
	(T
	 <COND (<F? .ADJ>
		<SET ADJ <RANDOM 4>>
		<COND (<1? .ADJ> <SET ADJ ,W?NORTH>)
		      (<==? .ADJ 2> <SET ADJ ,W?SOUTH>)
		      (<==? .ADJ 3> <SET ADJ ,W?EAST>)
		      (T <SET ADJ ,W?WEST>)>)>
	 <COND (<MC-IS? ,GL-PLACE-CUR ,FL-INDOORS>
		<COND (<==? .ADJ ,W?NORTH>
		       <COND (<MC-VERB? EXAMINE LOOK-ON>
			      <COND (<MC-HERE? ,RM-HOLMES-STUDY>
				     <TELL
"There is a large fireplace set into the north wall.">)>)>)
		      (<==? .ADJ ,W?SOUTH>
		       <COND (<MC-VERB? EXAMINE LOOK-ON>
			      <COND (<MC-HERE? ,RM-HOLMES-STUDY>
				     <TELL
"There is a patriotic \"V.R.\" spelled out in bullet marks
on the south wall." CR>)>)>)>)
	       (T
		<TELL ,K-NOT-SEE-HERE-MSG CR>)>)>>

<OBJECT TH-CROWDS
   (LOC GLOBAL-OBJECTS)
   (DESC "crowds")
   (FLAGS FL-PLURAL FL-HAS-DESCFCN)
   (SYNONYM CROWDS CROWD TOURISTS SHOPPERS WOMEN PEOPLE)
   (ACTION RT-AC-TH-CROWDS)>

<ROUTINE RT-AC-TH-CROWDS ("OPT" (CONTEXT <>))
  <COND (<OR <AND <FSET? ,GL-PLACE-CUR ,FL-INDOORS>
		  <NOT <MC-HERE? ,RM-BRITISH-MUSEUM>>>
	     <FSET? ,GL-PLACE-CUR ,FL-NO-CROWDS>>
	 <TELL "At last you've found some respite from the crowds." CR>
	 <RTRUE>)>
  <COND (<==? .CONTEXT ,K-M-DESCFCN>
	 <COND (<EQUAL? ,GL-PLACE-CUR ,RM-FLEET-STREET>
		<COND (<EQUAL? <RT-TIME-OF-DAY> 0>
		       <TELL "They're all inside, hard at work." CR>)
		      (T
		       <TELL
"They hurry past you, intent on getting to their offices." CR>)>)
	       (<EQUAL? <RT-TIME-OF-DAY> 0>
		<TELL "There are no crowds here. They're all asleep." CR>
		<RTRUE>)
	       (<EQUAL? ,GL-PLACE-CUR ,RM-TOTTENHAM-COURT-RD ,RM-OXFORD-ST>
		<TELL
"The shoppers don't appear happy that the shops are closed.">)
	       (<EQUAL? ,GL-PLACE-CUR ,RM-TRAFALGAR-SQUARE>
	        <TELL "The people are happily feeding the pigeons." CR>)
	       (<EQUAL? ,GL-PLACE-CUR ,RM-GROSVENOR-PLACE>
	        <TELL "The people are anxiously consulting their sight-seeing
maps." CR>)
	       (<EQUAL? ,GL-PLACE-CUR ,RM-THE-STRAND>
	        <COND (<EQUAL? <RT-TIME-OF-DAY> 3>
		       <TELL
"The theatre goers are happily discussing the evening's entertainment." CR>)
		      (T
		       <TELL
"The crowds are trying to select a show to attend this evening." CR>)>)
	       (T
		<COND (<EQUAL? <RT-TIME-OF-DAY> 2>
		       <TELL "They surge around you, excited about the
sight-seeing they have planned for today." CR>)
		      (<EQUAL? <RT-TIME-OF-DAY> 3>
		       <TELL "They nearly trample you in their eagerness
to get back to their hotels before darkness falls." CR>)
		      (<EQUAL? <RT-TIME-OF-DAY> 1>
		       <TELL "They surge around you, intent on their
sight-seeing." CR>)>
		<RTRUE>)>
	 <RTRUE>)
	(<EQUAL? <RT-TIME-OF-DAY> 0>
	 <TELL "There are no tourists here. They're all asleep." CR>)>>

<OBJECT TH-POCKET
   (LOC TH-WATSONS-COAT)
   (DESC "pocket")
   (FLAGS FL-SEEN FL-NODESC FL-CONTAINER FL-OPENED FL-YOUR FL-NOALL)
   (SYNONYM POCKET POCKETS)
   (ADJECTIVE MY)
   (SIZE 0 CAPACITY 10)
   (ACTION RT-AC-TH-POCKET)>

<OBJECT TH-GUN
   (LOC TH-MANTELPIECE)
   (DESC "pistol")
   (FLAGS FL-TAKEABLE FL-WEAPON FL-HAS-DESCFCN)
   (SYNONYM GUN PISTOL REVOLVER)
   (SIZE 5 MASS 10 CAPACITY 0)
   (ACTION RT-AC-TH-GUN)>

<OBJECT TH-MAIL
   (LOC TH-MANTELPIECE)
   (DESC "mail")
   (FLAGS FL-PLURAL FL-COLLECTIVE FL-TAKEABLE FL-READABLE
	  FL-BURNABLE FL-OPENABLE FL-HAS-DESCFCN)
   (SYNONYM MAIL LETTER LETTERS)
   (ADJECTIVE HOLMES\' HOLMES\'S)
   (SIZE 5)
   (ACTION RT-AC-TH-MAIL)>

<OBJECT TH-KNIFE
   (LOC TH-MANTELPIECE)
   (DESC "knife")
   (FLAGS FL-TAKEABLE FL-WEAPON FL-KNIFE FL-OPENABLE FL-OPENED
          FL-HAS-DESCFCN)
   (SYNONYM KNIFE JACKKNIFE)
   (SIZE 5)
   (ACTION RT-AC-TH-KNIFE)>

<OBJECT TH-MAGNIFYING-GLASS
   (LOC TH-BEDSIDE-TABLE)
   (DESC "magnifying glass")
   (FLAGS FL-TAKEABLE FL-TRANSPARENT FL-HAS-DESCFCN)
   (SYNONYM GLASS LENS)
   (ADJECTIVE MAGNIFYING HOLMES\' HOLMES\'S)
   (VALUE 1)
   (SIZE 5 MASS 4)
   (ACTION RT-AC-TH-MAGNIFYING-GLASS)>

<OBJECT TH-NEWSPAPER
   (LOC RM-HOLMES-STUDY)
   (DESC "newspaper")
   (FLAGS FL-TAKEABLE FL-READABLE FL-BURNABLE FL-SURFACE
	  FL-HAS-DESCFCN)
   (SYNONYM NEWSPAPER PAPER PAPERS)
   (ADJECTIVE NEWSPAPER NEWS)
   (VALUE 1)
   (SIZE 5 CAPACITY 10)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-NEWSPAPER)>

<OBJECT TH-VIOLIN
   (LOC RM-HOLMES-STUDY)
   (DESC "violin")
   (FLAGS FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM VIOLIN FIDDLE)
   (SIZE 5)
   (ACTION RT-AC-TH-VIOLIN)>

<OBJECT TH-WESTMINSTER-CLUE
   (LOC CH-PRIME-MINISTER)
   (DESC "clue paper")
   (FLAGS FL-TAKEABLE FL-READABLE FL-BURNABLE FL-HAS-DESCFCN)
   (SYNONYM PAPER PAPERS VERSE POEM)
   (ADJECTIVE CLUE)
   (SIZE 1)
   (FLIPPED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-WESTMINSTER-CLUE)>

<OBJECT TH-PHIAL
   (LOC CH-HOLMES)
   (DESC "phial")
   (FLAGS FL-TRYTAKE FL-CONTAINER FL-TRANSPARENT FL-NOALL)
   (SYNONYM PHIAL VIAL)
   (SIZE 1)
   (ACTION RT-AC-TH-PHIAL)>

<OBJECT TH-PERSIAN-SLIPPER
   (LOC TH-MANTELPIECE)
   (DESC "persian slipper")
   (FLAGS FL-TAKEABLE FL-CONTAINER FL-OPENED)
   (SYNONYM SLIPPER)
   (ADJECTIVE PERSIAN)
   (SIZE 5 CAPACITY 2)>

<OBJECT TH-PIPE
   (LOC TH-MANTELPIECE)
   (DESC "pipe")
   (FLAGS FL-TAKEABLE FL-CONTAINER FL-OPENED FL-HAS-DESCFCN)
   (SYNONYM PIPE)
   (VALUE 1)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-PIPE)>

<OBJECT TH-MATCH
   (LOC TH-MATCHBOOK)
   (FLAGS FL-TAKEABLE FL-BURNABLE FL-HAS-SDESC FL-HAS-DESCFCN)
   (SYNONYM MATCH)
   (ADJECTIVE ZZZP)
   (SIZE 1)
   (ACTION RT-AC-TH-MATCH)>

<OBJECT TH-BOAT
   (LOC RM-THE-EMBANKMENT)
   (DESC "old boat")
   (FLAGS FL-VOWEL FL-VEHICLE FL-CONTAINER FL-OPENED FL-HAS-DESCFCN)
   (SYNONYM BOAT ROWBOAT)
   (ADJECTIVE OLD)
   (SIZE 50 CAPACITY 999)
   (ACTION RT-AC-TH-BOAT)
   (CONTFCN RT-CF-TH-BOAT)>

<OBJECT TH-OAR-1
   (LOC TH-BOAT)
   (DESC "weathered old oar")
   (FLAGS FL-TAKEABLE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM OAR)
   (ADJECTIVE OLD WEATHERED)
   (SIZE 20)
   (GENERIC RT-GN-OAR)
   (ACTION RT-AC-TH-OAR-1)>

<OBJECT TH-OAR-2
   (LOC TH-BLIGH-BOAT)
   (DESC "Captain Bligh's oar")
   (FLAGS FL-NOARTC FL-TAKEABLE FL-NOALL)
   (SYNONYM OAR)
   (ADJECTIVE CAPTAIN\'S BLIGH\'S)
   (VALUE 1)
   (SIZE 20)
   (GENERIC RT-GN-OAR)>

<OBJECT TH-CLAPPER
   (LOC TH-BELL)
   (DESC "clapper")
   (FLAGS FL-NODESC FL-TRYTAKE FL-SURFACE FL-HAS-DESCFCN)
   (SYNONYM CLAPPER)
   (ACTION RT-AC-TH-CLAPPER)>

<OBJECT TH-COTTON-BALLS
   (LOC TH-BLUE-PILL-BOTTLE)
   (DESC "cotton balls")
   (FLAGS FL-PLURAL FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM BALL BALLS COTTONBALL)
   (ADJECTIVE COTTON)
   (SIZE 1)
   (ACTION RT-AC-TH-COTTON-BALLS)>

<OBJECT TH-BELL
   (LOC RM-CLOCK-TOWER)
   (DESC "Big Ben")
   (FLAGS FL-NODESC FL-NOARTC FL-TRYTAKE FL-CONTAINER FL-OPENED FL-NOALL
          FL-HAS-DESCFCN)
   (SYNONYM BEN BELL TOWER)
   (GENERIC RT-GN-TOWER)
   (ADJECTIVE BIG CLOCK BELL)
   (SIZE 50 MASS 50 CAPACITY 0)
   (ACTION RT-AC-TH-BELL)>

<OBJECT TH-STETHOSCOPE
   (LOC TH-WATSONS-HAT)
   (DESC "stethoscope")
   (FLAGS FL-CLOTHING FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM STETHOSCOPE SS STETH)
   (ADJECTIVE MY)
   (VALUE 1)
   (SIZE 1)
   (ACTION RT-AC-TH-STETHOSCOPE)>

<OBJECT TH-DIAL
   (LOC LG-BANK-VAULT-DOOR)
   (DESC "dial")
   (FLAGS FL-NODESC FL-READABLE FL-LOCKED)
   (SYNONYM DIAL)
   (ACTION RT-AC-TH-DIAL)>

<OBJECT TH-WATSONS-HAT
   (LOC CH-PLAYER)
   (DESC "hat")
   (FLAGS FL-TAKEABLE FL-SEEN FL-CLOTHING FL-WORN FL-CONTAINER
	  FL-YOUR FL-HAS-DESCFCN)
   (SYNONYM HAT BOWLER)
   (SIZE 5 CAPACITY 2)
   (ACTION RT-AC-TH-WATSONS-HAT)>

<OBJECT TH-ELIZAS-FLOWERS
   (LOC CH-ELIZA-DOOLITTLE)
   (DESC "flowers")
   (FLAGS FL-PLURAL FL-CONTAINER FL-OPENED FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM FLOWERS FLOWER CARNATION)
   (ADJECTIVE HER ELIZA\'S GIRL\'S)
   (SIZE 5)
   (ACTION RT-AC-TH-ELIZAS-FLOWERS)>

<OBJECT TH-CARNATION
   (DESC "carnation")
   (FLAGS FL-TAKEABLE FL-CLOTHING)
   (SYNONYM CARNATION FLOWER)
   (ADJECTIVE GIRL\'S ELIZA\'S)
   (VALUE 5)
   (SIZE 1 MASS 1)
   (ACTION RT-AC-TH-CARNATION)>

<OBJECT TH-SAPPHIRE
   (LOC TH-CLAPPER)
   (FLAGS FL-TAKEABLE FL-READABLE FL-NODESC FL-SURFACE FL-MONEY
	  FL-HAS-DESCFCN FL-HAS-SDESC FL-NEEDS-IDENTITY FL-LOSE-IDENTITY)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (SYNONYM SAPPHIRE GEM GEMS SCRATCH)
   (ADJECTIVE SAPPHIRE BLUE)
   (VALUE 5)
   (SIZE 1 CAPACITY 1)
   (GENERIC RT-GN-GEM)
   (ACTION RT-AC-TH-SAPPHIRE)>

<OBJECT TH-OPAL
   (DESC "opal")
   (FLAGS FL-VOWEL FL-TAKEABLE FL-READABLE FL-SURFACE FL-MONEY
	  FL-HAS-DESCFCN FL-HAS-SDESC FL-NEEDS-IDENTITY FL-LOSE-IDENTITY)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (SYNONYM OPAL GEM GEMS SCRATCH)
   (ADJECTIVE OPAL OPAL'S)
   (VALUE 5)
   (SIZE 1 MASS 1 CAPACITY 1)
   (GENERIC RT-GN-GEM)
   (ACTION RT-AC-TH-OPAL)>

<OBJECT TH-RUBY
   (LOC TH-NELSON-STATUE)
   (FLAGS FL-TAKEABLE FL-READABLE FL-SURFACE FL-MONEY FL-HAS-DESCFCN
	  FL-HAS-SDESC FL-NEEDS-IDENTITY FL-LOSE-IDENTITY)
   (SYNONYM RUBY GEM GEMS SCRATCH)
   (ADJECTIVE RUBY RED RUBY\'S)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (VALUE 5)
   (SIZE 1 MASS 1 CAPACITY 1)
   (GENERIC RT-GN-GEM)
   (ACTION RT-AC-TH-RUBY)>

<OBJECT TH-TOPAZ
   (LOC TH-SAFETY-DEPOSIT-BOX)
   (DESC "topaz")
   (FLAGS FL-TAKEABLE FL-READABLE FL-SURFACE FL-MONEY FL-HAS-DESCFCN
	  FL-HAS-SDESC FL-NEEDS-IDENTITY FL-LOSE-IDENTITY)
   (OBJ-ADJ 0)
   (OBJ-NOUN 0)
   (SYNONYM TOPAZ GEM GEMS SCRATCH)
   (ADJECTIVE TOPAZ TOPAZ\'S)
   (VALUE 5)
   (SIZE 1 MASS 1 CAPACITY 1)
   (GENERIC RT-GN-GEM)
   (ACTION RT-AC-TH-TOPAZ)>

<OBJECT TH-PACKET-OF-PAPER
   (LOC RM-NORTH-CLOISTER)
   (DESC "pacquet of paper")
   (FLAGS FL-TAKEABLE FL-CONTAINER FL-OPENABLE FL-READABLE)
   (SYNONYM PACK PAPER PACQUET PACKET BOX)
   (ADJECTIVE PACK PAPER PACKET PACQUET)
   (VALUE 1)
   (SIZE 5 CAPACITY 10)
   (GENERIC RT-GN-BOX)>

<OBJECT TH-GREEN-PAPER
   (LOC TH-PACKET-OF-PAPER)
   (DESC "green piece of paper")
   (FLAGS FL-TAKEABLE FL-SURFACE FL-READABLE FL-BURNABLE)
   (SYNONYM PAPER PAPERS BACK)
   (ADJECTIVE PIECE GREEN BACK)
   (SIZE 1 CAPACITY 11)
   (INSCRIPTION 0)
   (FLIPPED 0)
   (HEATED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-GREEN-PAPER)>

<OBJECT TH-WHITE-PAPER
   (LOC TH-PACKET-OF-PAPER)
   (DESC "white piece of paper")
   (FLAGS FL-TAKEABLE FL-SURFACE FL-READABLE FL-BURNABLE)
   (SYNONYM PAPER PAPERS BACK)
   (ADJECTIVE PIECE WHITE BACK)
   (SIZE 1 CAPACITY 11)
   (INSCRIPTION 0)
   (FLIPPED 0)
   (HEATED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-WHITE-PAPER)>

<OBJECT TH-BLUE-PAPER
   (LOC TH-PACKET-OF-PAPER)
   (DESC "blue piece of paper")
   (FLAGS FL-READABLE FL-SURFACE FL-TAKEABLE FL-BURNABLE)
   (SYNONYM PAPER PAPERS BACK)
   (ADJECTIVE PIECE BLUE BACK)
   (SIZE 1 CAPACITY 11)
   (INSCRIPTION 0)
   (FLIPPED 0)
   (HEATED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-BLUE-PAPER)>

<OBJECT TH-ORANGE-PAPER
   (LOC TH-PACKET-OF-PAPER)
   (DESC "orange piece of paper")
   (FLAGS FL-VOWEL FL-READABLE FL-SURFACE FL-TAKEABLE FL-BURNABLE)
   (SYNONYM PAPER PAPERS BACK)
   (ADJECTIVE PIECE ORANGE BACK)
   (SIZE 1 CAPACITY 11)
   (INSCRIPTION 0)
   (FLIPPED 0)
   (HEATED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-ORANGE-PAPER)>

<OBJECT TH-YELLOW-PAPER
   (LOC TH-PACKET-OF-PAPER)
   (DESC "yellow piece of paper")
   (FLAGS FL-READABLE FL-SURFACE FL-TAKEABLE FL-BURNABLE)
   (SYNONYM PAPER PAPERS BACK)
   (ADJECTIVE PIECE YELLOW BACK)
   (SIZE 1 CAPACITY 11)
   (INSCRIPTION 0)
   (FLIPPED 0)
   (HEATED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-YELLOW-PAPER)>

<OBJECT TH-BROWN-PAPER
   (LOC TH-PACKET-OF-PAPER)
   (DESC "brown piece of paper")
   (FLAGS FL-READABLE FL-SURFACE FL-TAKEABLE FL-BURNABLE)
   (SYNONYM PAPER PAPERS BACK)
   (ADJECTIVE PIECE BROWN BACK)
   (SIZE 1 CAPACITY 11)
   (INSCRIPTION 0)
   (FLIPPED 0)
   (HEATED 0)
   (GENERIC RT-GN-PAPER)
   (ACTION RT-AC-TH-BROWN-PAPER)>

<OBJECT TH-BANK-OF-CANDLES
   (LOC RM-EVANGELIST-CHAPEL)
   (DESC "bank of candles")
   (FLAGS FL-NODESC FL-LIGHTED FL-BURNABLE FL-TRYTAKE)
   (SYNONYM BANK CANDLES CANDLE FLAME)
   (ADJECTIVE BANK CANDLES)
   (ACTION RT-AC-TH-BANK-OF-CANDLES)>

<OBJECT TH-CRAYON
   (LOC RM-NORTH-CLOISTER)
   (DESC "crayon")
   (FLAGS FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM CRAYON)
   (VALUE 1)
   (SIZE 1)
   (ACTION RT-AC-TH-CRAYON)>

<OBJECT TH-WATSONS-COAT
   (LOC CH-PLAYER)
   (DESC "coat")
   (FLAGS FL-CLOTHING FL-SEEN FL-TAKEABLE FL-WORN FL-CONTAINER
	  FL-OPENED FL-YOUR FL-HAS-DESCFCN)
   (SYNONYM COAT)
   (SIZE 5 CAPACITY 15)
   (ACTION RT-AC-TH-WATSONS-COAT)>

<OBJECT TH-LAPEL
   (LOC TH-WATSONS-COAT)
   (DESC "lapel")
   (FLAGS FL-SEEN FL-NODESC FL-CONTAINER FL-OPENED FL-NOALL FL-TRYTAKE FL-YOUR)
   (SYNONYM LAPEL)
   (SIZE 3 CAPACITY 3)
   (ACTION RT-AC-TH-LAPEL)>

<OBJECT TH-EDWARD-TOMB
   (LOC RM-CONFESSOR-CHAPEL)
   (DESC "Edward the Confessor's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE
	  FL-VOWEL FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM TOMB CONFESSOR INSCRIPTION)
   (ADJECTIVE EDWARD EDWARD\'S)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-EDWARD-TOMB)>

<OBJECT TH-ELIZABETH-TOMB
   (LOC RM-NORTH-CHAPEL-AISLE)
   (DESC "Elizabeth's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE
	  FL-VOWEL FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION)
   (ADJECTIVE ELIZABETH MARY TUDOR)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-ELIZABETH-TOMB)>

<OBJECT TH-NEWTON-TOMB
   (LOC RM-NAVE)
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN FL-HAS-SDESC FL-NEEDS-IDENTITY FL-LOSE-IDENTITY)
   (OBJ-NOUN 0)
   (OBJ-ADJ 0)
   (SYNONYM TOMB INSCRIPTION SARCOPHAGUS STATUE CHERUB CHERUBS ANGELS ANGEL
	    DIAGRAM SYSTEM)
   (ADJECTIVE NEWTON\'S NEWTON SOLAR)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-NEWTON-TOMB)>

<OBJECT TH-HENRY-V-TOMB
   (LOC RM-CONFESSOR-CHAPEL)
   (DESC "Henry V's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION COFFIN EFFIGY)
   (ADJECTIVE HENRY HENRY\'S V V\'S)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-HENRY-V-TOMB)>

<OBJECT TH-POPE-TOMB
   (LOC RM-SOUTH-AISLE)
   (DESC "Alexander Pope's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION)
   (ADJECTIVE POPE POPE\'S)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-POPE-TOMB)>

<OBJECT TH-DICKENS-TOMB
   (LOC RM-SOUTH-TRANSEPT)
   (DESC "Charles Dickens's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION)
   (ADJECTIVE DICKENS DICKENS\')
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-DICKENS-TOMB)>

<OBJECT TH-CHAUCER-TOMB
   (LOC RM-POETS-CORNER)
   (DESC "Geoffrey Chaucer's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION SARCOPHAGUS)
   (ADJECTIVE CHAUCER CHAUCER\'S)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-CHAUCER-TOMB)>

<OBJECT TH-ANNE-OF-CLEVES-TOMB
   (LOC RM-SANCTUARY)
   (DESC "Anne of Cleve's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE
	  FL-VOWEL FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION ANNE CLEVES)
   (ADJECTIVE ANNE CLEVES CLEVES\' ANNE\'S ANNES)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-ANNE-OF-CLEVES-TOMB)>

<OBJECT TH-LITTLE-PRINCES-TOMB
   (LOC RM-INNOCENTS-CORNER)
   (DESC "tomb of the Little Princes in the Tower")
   (FLAGS FL-NODESC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB URN INSCRIPTION SARCOPHAGUS)
   (ADJECTIVE LITTLE PRINCES PRINCES\')
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-LITTLE-PRINCES-TOMB)>

<OBJECT TH-MARY-QUEEN-OF-SCOTS-TOMB
   (LOC RM-SOUTH-CHAPEL-AISLE)
   (DESC "Mary, Queen of Scots' tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION SARCOPHAGUS EFFIGY)
   (ADJECTIVE MARY SCOTS SCOTS\' MARY\'S QUEEN)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-MARY-QUEEN-OF-SCOTS-TOMB)>

<OBJECT TH-HENRY-VII-TOMB
   (LOC RM-HENRY-VII-CHAPEL)
   (DESC "Henry VII's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION)
   (ADJECTIVE HENRY VII HENRY\'S VII\'S ELIZABETH YORK)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-HENRY-VII-TOMB)>

<OBJECT TH-PITT-TOMB
   (LOC RM-NORTH-TRANSEPT)
   (DESC "Pitt the Younger's tomb")
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM TOMB INSCRIPTION)
   (ADJECTIVE PITT YOUNGER PITT\'S)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-PITT-TOMB)>

<OBJECT TH-FIGURES
   (LOC RM-NORTH-TRANSEPT)
   (DESC "allegorical figures")
   (FLAGS FL-NODESC FL-NOALL FL-VOWEL FL-PLURAL FL-HAS-DESCFCN)
   (SYNONYM FIGURES FIGURE)
   (ADJECTIVE ALLEGORICAL)
   (ACTION RT-AC-TH-FIGURES)>

<OBJECT TH-VERE-TOMB
   (LOC RM-EVANGELIST-CHAPEL)
   (FLAGS FL-NODESC FL-NOARTC FL-READABLE FL-SURFACE FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN FL-HAS-SDESC FL-NEEDS-IDENTITY FL-LOSE-IDENTITY)
   (SYNONYM TOMB INSCRIPTION EFFIGY KNIGHT KNIGHTS ARMOR ARMOUR SUIT) 
   (ADJECTIVE VERE VERE\'S SUIT ARMOUR ARMOR)
   (OBJ-NOUN 0)
   (OBJ-ADJ 0)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-VERE-TOMB)>

<OBJECT TH-LAMP
   (LOC TH-BEDSIDE-TABLE)
   (DESC "lamp")
   (FLAGS FL-LAMP FL-ASLEEP FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM LAMP LANTERN LIGHT)
   (ADJECTIVE BRASS LIGHT)
   (VALUE 3)
   (SIZE 5 MASS 4)
   (ACTION RT-AC-TH-LAMP)>

<OBJECT TH-CLUMP-OF-MOSS
   (LOC RM-THAMES-THREE)
   (DESC "clump of moss")
   (FLAGS FL-CONTAINER FL-NODESC FL-OPENED FL-TAKEABLE FL-TRYTAKE
	  FL-HAS-DESCFCN)
   (SYNONYM MOSS CLUMP)
   (ADJECTIVE CLUMP MOSS)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-CLUMP-OF-MOSS)>

<OBJECT TH-BLIGH-BOAT
   (LOC RM-THE-BLACK-MUSEUM)
   (DESC "Captain Bligh's boat")
   (FLAGS FL-NODESC FL-CONTAINER FL-OPENED FL-TRYTAKE FL-NOARTC FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM BOAT ROWBOAT)
   (ADJECTIVE BLIGH BLIGH\'S)
   (SIZE 50 CAPACITY 999)
   (ACTION RT-AC-TH-BLIGH-BOAT)>

<OBJECT TH-ANCHOR
   (LOC LG-WATER)
   (DESC "anchor")
   (FLAGS FL-TRYTAKE FL-TAKEABLE FL-VOWEL FL-HAS-DESCFCN)
   (SYNONYM ANCHOR)
   (SIZE 5 MASS 5)
   (ACTION RT-AC-TH-ANCHOR)>

<OBJECT TH-NELSON-STATUE
   (LOC TH-NELSON-COLUMN)
   (DESC "statue of Admiral Lord Nelson")
   (FLAGS FL-NODESC FL-CONTAINER FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM NELSON STATUE)
   (ADJECTIVE NELSON ADMIRAL LORD NELSON\'S)
   (SIZE 0 CAPACITY 5)
   (ACTION RT-AC-TH-NELSON-STATUE)>

<OBJECT TH-CROWN-JEWELS
   (LOC TH-MORIARTY-DESK)
   (DESC "the Crown Jewels")
   (FLAGS FL-PLURAL FL-TAKEABLE FL-NOARTC FL-TRYTAKE FL-MONEY FL-HAS-DESCFCN)
   (SYNONYM JEWELS REGALIA DIAMOND)
   (ADJECTIVE CROWN)
   (VALUE 10)
   (SIZE 10 MASS 10)
   (ACTION RT-AC-TH-CROWN-JEWELS)>

<OBJECT TH-LAIR-KEY
   (LOC TH-MORIARTY-DESK)
   (DESC "Moriarty's key")
   (FLAGS FL-KEY FL-TAKEABLE FL-NOARTC)
   (SYNONYM KEY)
   (ADJECTIVE MORIARTY)
   (SIZE 1)>

<OBJECT TH-ETHERIUM-AMPOULE
   (LOC TH-WORKBENCH)
   (FLAGS FL-CONTAINER FL-OPENABLE FL-TAKEABLE FL-VOWEL FL-READABLE
	  FL-HAS-SDESC)
   (SYNONYM AMPOULE ETHERIUM AMPULE LABEL)
   (ADJECTIVE ETHERIUM ETHERIUM)
   (GENERIC RT-GN-ETHERIUM)
   (SIZE 2 MASS 1)
   (ACTION RT-AC-TH-ETHERIUM-AMPOULE)>

<VOC "BROKEN" ADJ>

<OBJECT TH-LAB-EQUIPMENT
   (LOC TH-WORKBENCH)
   (DESC "laboratory equipment")
   (FLAGS FL-TRYTAKE FL-PLURAL FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM EQUIPMENT BEAKERS TESTUBES PIPETTES EXPERIMENT)
   (ADJECTIVE LABORATORY LAB)
   (ACTION RT-AC-TH-LAB-EQUIPMENT)>

; "TH-BROKEN-AMPOULE"

; "TH-ETHERIUM-GAS"

<OBJECT TH-ETHERIUM-GAS
   (LOC TH-ETHERIUM-AMPOULE)
   (DESC "etherium gas")
   (FLAGS FL-TRANSPARENT FL-NODESC FL-VOWEL FL-HAS-DESCFCN)
   (SYNONYM GAS ETHERIUM)
   (ADJECTIVE ETHERIUM GAS)
   (GENERIC RT-GN-ETHERIUM)
   (ACTION RT-AC-TH-ETHERIUM-GAS)>

<OBJECT TH-MANTELPIECE
   (LOC RM-HOLMES-STUDY)
   (DESC "mantelpiece")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL)
   (SYNONYM MANTELPIECE MANTEL MANTLEPIECE MANTLE)
   (SIZE 50 CAPACITY 50)>

<OBJECT TH-WORKBENCH
   (LOC RM-HOLMES-BEDROOM)
   (DESC "workbench")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL)
   (SYNONYM WORKBENCH BENCH)
   (ADJECTIVE WORK)
   (SIZE 50 CAPACITY 50)>

<OBJECT TH-BLACK-BAG
   (LOC CH-PLAYER)
   (DESC "black bag")
   (FLAGS FL-CONTAINER FL-SEEN FL-OPENABLE FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM BAG)
   (ADJECTIVE BLACK MEDICAL)
   (SIZE 15 CAPACITY 999)
   (ACTION RT-AC-TH-BLACK-BAG)>

<OBJECT TH-CHARLES-STATUE
   (LOC RM-CHAMBER-OF-HORRORS)
   (DESC "statue of Charles I")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM CHARLES CHARLES\' STATUE I WAX)
   (ADJECTIVE CHARLES WAX)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-CHARLES-STATUE)>

<OBJECT TH-WILLIAM-STATUE
   (LOC RM-MADAME-TUSSAUDS)
   (DESC "statue of William the Conqueror")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM WILLIAM CONQUEROR STATUE WAX)
   (ADJECTIVE WILLIAM WAX WILLIAM\'S)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-WILLIAM-STATUE)>

<OBJECT TH-WALTER-STATUE
   (LOC RM-MADAME-TUSSAUDS)
   (DESC "statue of Sir Walter Raleigh")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM WALTER RALEIGH STATUE WAX)
   (ADJECTIVE SIR WALTER RALEIGH WAX WALTER\'S RALEIGH\'S)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-WALTER-STATUE)>

<OBJECT TH-ELIZABETH-STATUE
   (LOC RM-MADAME-TUSSAUDS)
   (DESC "figure of Queen Elizabeth")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM FIGURE ELIZABETH WAX)
   (ADJECTIVE QUEEN)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-ELIZABETH-STATUE)>

<OBJECT TH-THOMAS-STATUE
   (LOC RM-CHAMBER-OF-HORRORS)
   (DESC "statue of Saint Thomas Becket")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM THOMAS BECKET STATUE WAX)
   (ADJECTIVE SAINT THOMAS BECKET WAX BECKET\'S)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-THOMAS-STATUE)>

<OBJECT TH-DRAKE-STATUE
   (LOC RM-MADAME-TUSSAUDS)
   (DESC "statue of Sir Francis Drake")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM FRANCIS DRAKE STATUE WAX)
   (ADJECTIVE SIR FRANCIS DRAKE WAX DRAKE\'S)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-DRAKE-STATUE)>

<OBJECT TH-JOHN-STATUE
   (LOC RM-MADAME-TUSSAUDS)
   (DESC "statue of King John")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM JOHN STATUE WAX)
   (ADJECTIVE KING JOHN WAX JOHN\'S)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-JOHN-STATUE)>

<OBJECT TH-FAWKES-STATUE
   (LOC RM-CHAMBER-OF-HORRORS)
   (DESC "statue of Guy Fawkes")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM FAWKES STATUE WAX)
   (ADJECTIVE GUY WAX FAWKES\' GUY\'S)
   (GENERIC RT-GN-WAX)
   (ACTION RT-AC-TH-FAWKES-STATUE)>

<OBJECT TH-NELSON-COLUMN
   (LOC RM-TRAFALGAR-SQUARE)
   (DESC "Nelson's Column")
   (FLAGS FL-NODESC FL-READABLE FL-SURFACE FL-NOARTC FL-TRYTAKE FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM COLUMN MONUMENT)
   (ADJECTIVE NELSON NELSON\'S)
   (SIZE 100 MASS 100 CAPACITY 100)
   (ACTION RT-AC-TH-NELSON-COLUMN)>

<OBJECT TH-ROPE
   (LOC RM-LAIR)
   (DESC "rope")
   (FLAGS FL-TAKEABLE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM ROPE)
   (SIZE 15 MASS 3)
   (ACTION RT-AC-TH-ROPE)>

<OBJECT TH-LIQUID
   (LOC TH-PHIAL)
   (DESC "clear, colourless liquid")
   (FLAGS FL-HAS-DESCFCN)
   (SYNONYM LIQUID DRUG COCAINE)
   (ADJECTIVE CLEAR COLOURLESS)
   (SIZE 1)
   (ACTION RT-AC-TH-LIQUID)>

<OBJECT TH-SYRINGE
   (LOC TH-MOROCCO-CASE)
   (DESC "syringe")
   (SYNONYM SYRINGE NEEDLE)
   (ADJECTIVE HYPODERMIC)
   (SIZE 1 CAPACITY 5)
   (ACTION RT-AC-TH-SYRINGE)>

<OBJECT TH-MOROCCO-CASE
   (LOC TH-SOFA)
   (FLAGS FL-OPENED FL-CONTAINER FL-NOALL FL-TAKEABLE FL-OPENABLE)
   (DESC "morocco case")
   (SYNONYM CASE)
   (ADJECTIVE MOROCCO)
   (SIZE 5)
   (ACTION RT-AC-TH-MOROCCO-CASE)>

<OBJECT TH-DEAD-PIGEON
   (DESC "dead pigeon")
   (FLAGS FL-TAKEABLE)
   (SYNONYM PIGEON BIRD)
   (ADJECTIVE DEAD)
   (SIZE 5 CAPACITY 0 MASS 5)
   (VALUE 0)
   (GENERIC RT-GN-PIGEON)
   (ACTION RT-AC-TH-DEAD-PIGEON)>

<OBJECT TH-RED-GLASS
   (DESC "red piece of glass")
   (FLAGS FL-TAKEABLE FL-HAS-DESCFCN)
   (SYNONYM GLASS)
   (ADJECTIVE RED)
   (SIZE 5)
   (ACTION RT-AC-TH-RED-GLASS)>

<OBJECT TH-PIGEONS
   (LOC RM-TRAFALGAR-SQUARE)
   (DESC "pigeons")
   (FLAGS FL-NODESC FL-PLURAL FL-ALIVE FL-TRYTAKE FL-HAS-DESCFCN)
   (SYNONYM PIGEONS BIRDS)
   (ADJECTIVE FLOCK GROUP)
   (SIZE 50 MASS 250)
   (ACTION RT-AC-TH-PIGEONS)>

<OBJECT TH-BEDSIDE-TABLE
   (LOC RM-HOLMES-BEDROOM)
   (DESC "bedside table")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE)
   (SYNONYM TABLE)
   (ADJECTIVE BEDSIDE)
   (SIZE 15 CAPACITY 50)>

<OBJECT TH-BED
   (LOC RM-HOLMES-BEDROOM)
   (DESC "bed")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL)
   (SYNONYM BED)
   (ADJECTIVE SHERLOCK HOLMES\' HOLMES\'S)
   (SIZE 15 CAPACITY 999)
   (ACTION RT-AC-TH-BED)>

<OBJECT TH-MATCHBOOK
   (LOC RM-PARLOUR)
   (DESC "matchbook")
   (FLAGS FL-CONTAINER FL-TAKEABLE FL-OPENABLE FL-READABLE FL-BURNABLE)
   (SYNONYM BOOK MATCHES MATCHBOOK)
   (VALUE 1)
   (SIZE 5 CAPACITY 1)
   (ACTION RT-AC-TH-MATCHBOOK)>

<OBJECT TH-PADDLE
   (LOC RM-INSIDE-TRAITORS-GATE)
   (DESC "paddle")
   (FLAGS FL-TAKEABLE)
   (SYNONYM PADDLE)
   (VALUE 1)
   (SIZE 15)>

<OBJECT TH-OAR-LOCK
   (LOC TH-BOAT)
   (DESC "oarlock")
   (FLAGS FL-NODESC FL-CONTAINER FL-OPENED)
   (SYNONYM OARLOCK)
   (SIZE 0 CAPACITY 100)
   (ACTION RT-AC-TH-OAR-LOCK)>

<OBJECT TH-CLUE-BOOK
   (LOC TH-LIBRARY-TABLE)
   (DESC "old book")
   (FLAGS FL-CONTAINER FL-OPENABLE FL-TAKEABLE FL-SURFACE FL-BURNABLE
	  FL-READABLE FL-HAS-DESCFCN FL-VOWEL)
   (SYNONYM BOOK)
   (ADJECTIVE OLD)
   (SIZE 5)
   (ACTION RT-AC-TH-CLUE-BOOK)>

<OBJECT TH-BOOK-PAGE
   (LOC TH-CLUE-BOOK)
   (DESC "book page")
   (FLAGS FL-READABLE FL-BURNABLE FL-SURFACE FL-NODESC FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM PAGE INTRODUCTION)
   (ADJECTIVE BOOK)
   (SIZE 2)
   (ACTION RT-AC-TH-BOOK-PAGE)>

<OBJECT TH-PRAMS
   (LOC RM-KENSINGTON-GARDENS)
   (DESC "prams")
   (FLAGS FL-CONTAINER FL-OPENABLE FL-NODESC FL-PLURAL)
   (SYNONYM PRAMS PERAMBULATOR PRAM)
   (SIZE 0 CAPACITY 20)
   (ACTION RT-AC-TH-PRAMS)>

<OBJECT TH-PARK-FLOWERS
   (LOC RM-KENSINGTON-GARDENS)
   (DESC "flowers")
   (FLAGS FL-TRYTAKE FL-NODESC FL-PLURAL FL-HAS-DESCFCN)
   (SYNONYM FLOWERS FLOWER ROSE ROSES DAISIES PETUNIAS
            PEONIES MARIGOLDS DAFFODILS)
   (ACTION RT-AC-TH-PARK-FLOWERS)>

<OBJECT TH-SHAFT
   (LOC RM-CLOCK-TOWER)
   (DESC "shaft")
   (FLAGS FL-NODESC FL-CONTAINER FL-NOALL)
   (SYNONYM SHAFT)
   (SIZE 0 CAPACITY 999)
   (ACTION RT-AC-TH-SHAFT)>

<OBJECT TH-LIBRARY-TABLE
   (LOC RM-MANUSCRIPT-ROOM)
   (DESC "table")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE)
   (SYNONYM TABLE)
   (SIZE 15 CAPACITY 50)>

<OBJECT TH-JEWEL-CASE
   (LOC RM-JEWEL-ROOM)
   (DESC "jewel case")
   (FLAGS FL-NODESC FL-CONTAINER FL-TRYTAKE FL-OPENED FL-NOALL
	  FL-HAS-DESCFCN)
   (SYNONYM CASE)
   (ADJECTIVE JEWEL)
   (SIZE 15 CAPACITY 50)
   (ACTION RT-AC-TH-JEWEL-CASE)>

<OBJECT TH-ELIZAS-MOUTH
   (LOC CH-ELIZA-DOOLITTLE)
   (DESC "girl's mouth")
   (FLAGS FL-NODESC FL-CONTAINER FL-OPENED FL-OPENABLE)
   (SYNONYM MOUTH)
   (ADJECTIVE HER ELIZA\'S GIRL\'S)
   (SIZE 0 CAPACITY 1)
   (GENERIC RT-GN-BODY)
   (ACTION RT-AC-TH-ELIZAS-MOUTH)>

<OBJECT TH-RUBBINGS
   (LOC RM-NORTH-CLOISTER)
   (DESC "rubbings")
   (FLAGS FL-NODESC FL-PLURAL FL-READABLE FL-HAS-DESCFCN)
   (SYNONYM RUBBING RUBBINGS)
   (ADJECTIVE BRASS)
   (ACTION RT-AC-TH-RUBBINGS)>

<OBJECT TH-SOFA
   (LOC RM-HOLMES-STUDY)
   (DESC "sofa")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL)
   (SYNONYM SOFA COUCH)
   (SIZE 15 CAPACITY 999)
   (ACTION RT-AC-TH-SOFA)>

<OBJECT TH-ASH
   (LOC RM-MADAME-TUSSAUDS)
   (DESC "cigar ash")
   (FLAGS FL-NODESC FL-NOALL FL-ASLEEP FL-TAKEABLE FL-VOWEL FL-PLURAL
          FL-COLLECTIVE FL-HAS-DESCFCN)
   (SYNONYM SPECK ASH)
   (ADJECTIVE TRICHINOPOLY FLAKE CIGAR SPECK MY)
   (ACTION RT-AC-TH-ASH)>

<OBJECT TH-CIGARETTE
   (LOC CH-TUSSAUDS-GUARD)
   (DESC "cigarette")
   (FLAGS FL-LIGHTED FL-BURNABLE FL-TRYTAKE)
   (SYNONYM CIGARETTE CIGARET)
   (ADJECTIVE GUARD\'S)
   (ACTION RT-AC-TH-CIGARETTE)>

<OBJECT TH-TURBAN
   (LOC CH-AKBAR)
   (DESC "turban")
   (FLAGS FL-NODESC FL-NOALL)
   (SYNONYM TURBAN)
   (ACTION RT-AC-TH-TURBAN)>

<OBJECT TH-ANCHOR-CHAIN
   (LOC TH-BOAT)
   (DESC "anchor chain")
   (FLAGS FL-NODESC FL-CONTAINER FL-OPENED FL-VOWEL)
   (SYNONYM CHAIN)
   (SIZE 5)
   (ADJECTIVE ANCHOR)
   (ACTION RT-AC-TH-ANCHOR-CHAIN)>

<OBJECT TH-RAILING
   (LOC RM-CLOCK-TOWER)
   (DESC "railing")
   (FLAGS FL-NODESC FL-SURFACE FL-TRYTAKE FL-NOALL)
   (SYNONYM RAILING RAIL)
   (SIZE 50 CAPACITY 50)
   (ACTION RT-AC-TH-RAILING)>

<OBJECT TH-OPIUM-GAS
   (LOC RM-BAR-OF-GOLD)
   (DESC "opium")
   (FLAGS FL-TRANSPARENT FL-NODESC FL-VOWEL FL-BUYABLE FL-PLURAL FL-COLLECTIVE
	  FL-HAS-DESCFCN)
   (SYNONYM GAS OPIUM FUMES)
   (ACTION RT-AC-TH-OPIUM-GAS)>

<OBJECT TH-TRINKETS
   (LOC CH-VENDOR)
   (DESC "trinkets")
   (FLAGS FL-TRYTAKE FL-BUYABLE FL-PLURAL FL-NODESC FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM TRINKETS BAUBLES TRINKET)
   (SIZE 15)
   (ACTION RT-AC-TH-TRINKETS)>

<OBJECT TH-GUARDS-KEYS
   (LOC TH-GUARDS-POCKET)
   (DESC "keys")
   (FLAGS FL-TAKEABLE FL-PLURAL FL-HAS-DESCFCN)
   (SYNONYM KEYS)
   (ADJECTIVE HIS GUARD\'S)
   (ACTION RT-AC-TH-GUARDS-KEYS)>

<OBJECT TH-MORIARTY-DESK
   (LOC RM-LAIR)
   (DESC "desk")
   (FLAGS FL-TRYTAKE FL-NOALL FL-NODESC FL-SURFACE)
   (SYNONYM DESK TABLE)
   (ADJECTIVE MORIARTY)
   (SIZE 15 CAPACITY 50)>

<OBJECT TH-NELSON-EYE
   (LOC TH-NELSON-COLUMN)
   (DESC "Nelson's blind eye")
   (FLAGS FL-NOARTC FL-NODESC FL-TRYTAKE FL-NOALL FL-HAS-DESCFCN)
   (SYNONYM EYE)
   (ADJECTIVE NELSON ADMIRAL LORD NELSON\'S BLIND)
   (ACTION RT-AC-TH-NELSON-EYE)>

;<OBJECT TH-UNINTERESTING-OBJECT
   (LOC RM-SIGN-OF-RED-HERRING)
   (DESC "uninteresting object")
   (FLAGS FL-NODESC FL-TRYTAKE FL-HAS-DESCFCN)
   (SYNONYM OBJECT)
   (ADJECTIVE UNINTERESTING)
   (ACTION RT-AC-TH-UNINTERESTING-OBJECT)>
